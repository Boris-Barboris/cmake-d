# Adding D unittests
#
#  Copyright (c) 2010 Jens Mueller <jens.k.mueller@gmx.de>
#
# All rights reserved.
#
# See LICENSE for details.
#

macro(target_add_unittests _target _sourceFileList)
    set(_testtarget "test_${_target}")

    set(main_unittest "${CMAKE_CURRENT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/unittest.d")
    if(NOT EXISTS ${main_unittest})
        file(WRITE ${main_unittest} "// Generated by UseDUnittest.cmake\nint main() { return 0; }")
    endif()
   
    string(REPLACE " " ";" FLAGS ${CMAKE_D_FLAGS_DEBUG_INIT})

    get_target_property(include_dirs ${_target} INCLUDE_DIRECTORIES)
    set(INCLUDES )
    foreach(include_dir ${include_dirs})
        if(include_dir)
            list(APPEND INCLUDES "${CMAKE_INCLUDE_FLAG_D}${include_dir}")
        endif()
    endforeach()

    get_target_property(libs ${_target} LINK_LIBRARIES)
    set(LIBS )
    foreach(lib ${libs})
        if(lib)
            list(APPEND LIBS "${CMAKE_LINK_LIBRARY_FLAG}${lib}")
        endif()
    endforeach()

    set(SOURCES )
    foreach(src ${_sourceFileList})
        list(APPEND SOURCES "${CMAKE_CURRENT_SOURCE_DIR}/${src}")
    endforeach()

    add_test(NAME build_${_testtarget}
        COMMAND ${CMAKE_D_COMPILER} ${FLAGS} ${INCLUDES} ${CMAKE_LIBRARY_PATH_FLAG}${CMAKE_LIBRARY_OUTPUT_DIRECTORY} ${LIBS} -of${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${_testtarget} ${SOURCES} -unittest ${main_unittest})

    add_test(NAME ${_testtarget}
        COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${_testtarget})
    set_tests_properties(${_testtarget} PROPERTIES ENVIRONMENT "LD_LIBRARY_PATH=${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_LIBRARY_OUTPUT_DIRECTORY}")
endmacro()
